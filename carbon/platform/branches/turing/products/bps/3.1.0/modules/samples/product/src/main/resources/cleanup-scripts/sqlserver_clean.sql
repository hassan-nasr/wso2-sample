IF (OBJECT_ID('cleanInstance') IS NOT NULL)
  DROP PROCEDURE cleanInstance
GO

CREATE PROCEDURE cleanInstance(@inst_state INT, @lastActive DATETIME)
AS
BEGIN
	DECLARE @ID varchar(255)
	DECLARE @test_cus CURSOR
	SET @test_cus = CURSOR FOR SELECT ID FROM ODE_PROCESS_INSTANCE WHERE INSTANCE_STATE = @inst_state AND LAST_ACTIVE_TIME < @lastActive
	OPEN @test_cus
	FETCH NEXT
	FROM @test_cus INTO @ID
	WHILE @@FETCH_STATUS = 0
	BEGIN
		PRINT ' Start deleting instance data with instance id ' + @ID
		BEGIN TRANSACTION;
		DELETE FROM ODE_EVENT WHERE INSTANCE_ID = @ID;
		DELETE FROM ODE_CORSET_PROP WHERE CORRSET_ID IN (SELECT cs.CORRELATION_SET_ID FROM ODE_CORRELATION_SET cs WHERE cs.SCOPE_ID IN (SELECT os.SCOPE_ID FROM ODE_SCOPE os WHERE os.PROCESS_INSTANCE_ID = @ID));
		DELETE FROM ODE_CORRELATION_SET WHERE SCOPE_ID IN (SELECT os.SCOPE_ID FROM ODE_SCOPE os WHERE os.PROCESS_INSTANCE_ID = @ID);
		DELETE FROM ODE_PARTNER_LINK WHERE SCOPE_ID IN (SELECT os.SCOPE_ID FROM ODE_SCOPE os WHERE os.PROCESS_INSTANCE_ID = @ID);
		DELETE FROM ODE_XML_DATA_PROP WHERE XML_DATA_ID IN (SELECT xd.XML_DATA_ID FROM ODE_XML_DATA xd WHERE xd.SCOPE_ID IN (SELECT os.SCOPE_ID FROM ODE_SCOPE os WHERE os.PROCESS_INSTANCE_ID = @ID));
		DELETE FROM ODE_XML_DATA WHERE SCOPE_ID IN (SELECT os.SCOPE_ID FROM ODE_SCOPE os WHERE os.PROCESS_INSTANCE_ID = @ID);
		DELETE FROM ODE_SCOPE WHERE PROCESS_INSTANCE_ID = @ID;
		DELETE FROM ODE_MEX_PROP WHERE MEX_ID IN (SELECT mex.MESSAGE_EXCHANGE_ID FROM ODE_MESSAGE_EXCHANGE mex WHERE mex.PROCESS_INSTANCE_ID = @ID);
		DELETE FROM ODE_MESSAGE WHERE MESSAGE_EXCHANGE_ID IN (SELECT mex.MESSAGE_EXCHANGE_ID FROM ODE_MESSAGE_EXCHANGE mex WHERE mex.PROCESS_INSTANCE_ID = @ID);
		DELETE FROM ODE_MESSAGE_EXCHANGE WHERE PROCESS_INSTANCE_ID = @ID;
		DELETE FROM ODE_MESSAGE_ROUTE WHERE PROCESS_INSTANCE_ID = @ID;
		DELETE FROM ODE_PROCESS_INSTANCE WHERE ID = @ID;
		COMMIT;
		PRINT ' End deleting instance data with instance id ' + @ID
		FETCH NEXT
		FROM @test_cus INTO @ID
	END
CLOSE @test_cus
DEALLOCATE @test_cus
END
GO


DECLARE @I_STATE INT;
SET @I_STATE = 30;
DECLARE @L_ACTIVE DATETIME;
SET @L_ACTIVE=CURRENT_TIMESTAMP-2;

BEGIN
  PRINT (' Starting cleanInstance procedure ');
  EXEC cleanInstance @I_STATE,@L_ACTIVE;
  PRINT (' Ending cleanInstance procedure '); 
END